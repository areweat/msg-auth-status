authres-header-field = "Authentication-Results:" authres-payload

authres-payload = [CFWS] authserv-id
         [ CFWS authres-version ]
         ( no-result / 1*resinfo ) [CFWS] CRLF

authserv-id = value
            ; see below for a description of this element

authres-version = 1*DIGIT [CFWS]
        ; indicates which version of this specification is in use;
        ; this specification is version "1", and the absence of a
        ; version implies this version of the specification

no-result = [CFWS] ";" [CFWS] "none"
          ; the special case of "none" is used to indicate that no
          ; message authentication was performed

resinfo = [CFWS] ";" methodspec [ CFWS reasonspec ]
          [ CFWS 1*propspec ]

methodspec = [CFWS] method [CFWS] "=" [CFWS] result
           ; indicates which authentication method was evaluated
           ; and what its output was

reasonspec = "reason" [CFWS] "=" [CFWS] value
           ; a free-form comment on the reason the given result
           ; was returned

propspec = ptype [CFWS] "." [CFWS] property [CFWS] "=" pvalue
         ; an indication of which properties of the message
         ; were evaluated by the authentication scheme being
         ; applied to yield the reported result
method = Keyword [ [CFWS] "/" [CFWS] method-version ]
       ; a method indicates which method's result is
       ; represented by "result"; it is one of the methods
       ; explicitly defined as valid in this document
       ; or is an extension method as defined below

method-version = 1*DIGIT [CFWS]
       ; indicates which version of the method specification is
       ; in use, corresponding to the matching entry in the IANA
       ; "Email Authentication Methods" registry; a value of "1"
       ; is assumed if this version string is absent

result = Keyword
       ; indicates the results of the attempt to authenticate
       ; the message; see below for details

ptype = Keyword
      ; indicates whether the property being evaluated was
      ; a parameter to an SMTP command [SMTP], was a value taken
      ; from a message header field, was some property of
      ; the message body, or was some other property evaluated by
      ; the receiving MTA; expected to be one of the "property
      ; types" explicitly defined as valid, or an extension
      ; ptype, as defined below

property = special-smtp-verb / Keyword
        ; indicates more specifically than "ptype" what the
        ; source of the evaluated property is; the exact meaning
        ; is specific to the method whose result is being reported
        ; and is defined more clearly below

special-smtp-verb = "mailfrom" / "rcptto"
        ; special cases of SMTP commands [SMTP] that are made up
        ; of multiple words

pvalue = [CFWS] ( value / [ [ local-part ] "@" ] domain-name )
         [CFWS]
       ; the value extracted from the message property defined
       ; by the "ptype.property" construction